

# COUPLINGS_AC_CONFIG_PUBL_INIT([OUPUT FILE NAME])
#-------------------------------------------
# Initialize file

AC_DEFUN([COUPLINGS_AC_CONFIG_PUBL_INIT],[

# First arg is output file name
if test "$1" = "" ; then
  couplings_ac_config_publ_h="config_publ.h"
else
  couplings_ac_config_publ_h=$1
fi
couplings_ac_lower='abcdefghijklmnopqrstuvwxyz.'
couplings_ac_upper='ABCDEFGHIJKLMNOPQRSTUVWXYZ_'
couplings_ac_config_publ_upname=`echo $couplings_ac_config_publ_h | sed y/$couplings_ac_lower/$couplings_ac_upper/`
unset couplings_ac_lower
unset couplings_ac_upper
echo "#ifndef __"${couplings_ac_config_publ_upname}"__" >  "$couplings_ac_config_publ_h"-tmp
echo "#define __"${couplings_ac_config_publ_upname}"__" >> "$couplings_ac_config_publ_h"-tmp
echo >> "$couplings_ac_config_publ_h"-tmp
echo "/* $couplings_ac_config_publ_h. Generated by configure */" >> "$couplings_ac_config_publ_h"-tmp
echo >> "$couplings_ac_config_publ_h"-tmp

AC_MSG_NOTICE([initializing $couplings_ac_config_publ_h])
])dnl


# COUPLINGS_AC_CONFIG_PUBL_FINALIZE
#----------------------------
# Finalize file

AC_DEFUN([COUPLINGS_AC_CONFIG_PUBL_FINALIZE],[

AC_REQUIRE([COUPLINGS_AC_CONFIG_PUBL_INIT])dnl

echo "#endif /* __"${couplings_ac_config_publ_upname}"__ */" >>  "$couplings_ac_config_publ_h"-tmp

AC_MSG_NOTICE([closing $couplings_ac_config_publ_h])

diff $couplings_ac_config_publ_h $couplings_ac_config_publ_h-tmp  > /dev/null 2>&1
if test $? -eq 0 ; then
  AC_MSG_NOTICE([$couplings_ac_config_publ_h  is unchanged])
  rm -f $couplings_ac_config_publ_h-tmp
else
  mv $couplings_ac_config_publ_h-tmp $couplings_ac_config_publ_h
fi

unset couplings_ac_config_publ_h
unset couplings_ac_config_publ_upname

])dnl


# COUPLINGS_AC_CONFIG_PUBL_VERBATIM([VERBATIM TEXT])
#---------------------------------------------
# Add text to config file

AC_DEFUN([COUPLINGS_AC_CONFIG_PUBL_VERBATIM],[

AC_REQUIRE([COUPLINGS_AC_CONFIG_PUBL_INIT])dnl

echo "$1" >> "$couplings_ac_config_publ_h"-tmp
echo ""   >> "$couplings_ac_config_publ_h"-tmp

])dnl


# COUPLINGS_AC_CONFIG_PUBL_DEFINE(VARIABLE NAME, VALUE, [COMMENT])
#-----------------------------------------------------------
# Define variable

AC_DEFUN([COUPLINGS_AC_CONFIG_PUBL_DEFINE],[

AC_REQUIRE([COUPLINGS_AC_CONFIG_PUBL_INIT])dnl

unset couplings_ac_lower
unset couplings_ac_upper
if test "$3" != "" ; then
  echo "/* $3 */" >> "$couplings_ac_config_publ_h"-tmp
fi
echo "#define $1 $2" >> "$couplings_ac_config_publ_h"-tmp
echo >> "$couplings_ac_config_publ_h"-tmp

])dnl


# COUPLINGS_AC_CONFIG_PUBL_DEFINE_STRING(VARIABLE NAME, VALUE, [COMMENT])
#------------------------------------------------------------------
# Define string variable

AC_DEFUN([COUPLINGS_AC_CONFIG_PUBL_DEFINE_STRING],[

AC_REQUIRE([COUPLINGS_AC_CONFIG_PUBL_INIT])dnl

unset couplings_ac_lower
unset couplings_ac_upper
if test "$3" != "" ; then
  echo "/* $3 */" >> "$couplings_ac_config_publ_h"-tmp
fi
echo '#define $1 "$2"' >> "$couplings_ac_config_publ_h"-tmp
echo >> "$couplings_ac_config_publ_h"-tmp

])dnl


# COUPLINGS_AC_CONFIG_PUBL_SET(VARIABLE NAME, YES OR NO, [COMMENT])
#------------------------------------------------------------
# Set variable (define to 1 if second arg is yes, undefine if no )

AC_DEFUN([COUPLINGS_AC_CONFIG_PUBL_SET],[

# First arg is variable name, second arg is value, optional third arg is comment

AC_REQUIRE([COUPLINGS_AC_CONFIG_PUBL_INIT])dnl

unset couplings_ac_lower
unset couplings_ac_upper
if test "$3" != "" ; then
  echo "/* $3 */" >> "$couplings_ac_config_publ_h"-tmp
fi
if test "$2" = "yes" ; then
  echo "#define $1 1" >> "$couplings_ac_config_publ_h"-tmp
else
  echo "#undef $1" >> "$couplings_ac_config_publ_h"-tmp
fi
echo >> "$couplings_ac_config_publ_h"-tmp

])dnl

