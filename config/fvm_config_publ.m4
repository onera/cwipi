dnl Copyright (C) 2004-2006 EDF
dnl
dnl This file is part of the FVM software package.  For license
dnl information, see the COPYING file in the top level directory of the
dnl FVM source distribution.


# FVMC_AC_CONFIG_PUBL_INIT([OUPUT FILE NAME])
#-------------------------------------------
# Initialize file

AC_DEFUN([FVMC_AC_CONFIG_PUBL_INIT],[

# First arg is output file name
if test "$1" = "" ; then
  fvmc_ac_config_publ_h="config_publ.h"
else
  fvmc_ac_config_publ_h=$1
fi
fvmc_ac_lower='abcdefghijklmnopqrstuvwxyz.'
fvmc_ac_upper='ABCDEFGHIJKLMNOPQRSTUVWXYZ_'
fvmc_ac_config_publ_upname=`echo $fvmc_ac_config_publ_h | sed y/$fvmc_ac_lower/$fvmc_ac_upper/`
unset fvmc_ac_lower
unset fvmc_ac_upper
echo "#ifndef __"${fvmc_ac_config_publ_upname}"__" >  "$fvmc_ac_config_publ_h"-tmp
echo "#define __"${fvmc_ac_config_publ_upname}"__" >> "$fvmc_ac_config_publ_h"-tmp
echo >> "$fvmc_ac_config_publ_h"-tmp
echo "/* $fvmc_ac_config_publ_h. Generated by configure */" >> "$fvmc_ac_config_publ_h"-tmp
echo >> "$fvmc_ac_config_publ_h"-tmp

AC_MSG_NOTICE([initializing $fvmc_ac_config_publ_h])
])dnl


# FVMC_AC_CONFIG_PUBL_FINALIZE
#----------------------------
# Finalize file

AC_DEFUN([FVMC_AC_CONFIG_PUBL_FINALIZE],[

AC_REQUIRE([FVMC_AC_CONFIG_PUBL_INIT])dnl

echo "#endif /* __"${fvmc_ac_config_publ_upname}"__ */" >>  "$fvmc_ac_config_publ_h"-tmp

AC_MSG_NOTICE([closing $fvmc_ac_config_publ_h])

diff $fvmc_ac_config_publ_h $fvmc_ac_config_publ_h-tmp  > /dev/null 2>&1
if test $? -eq 0 ; then
  AC_MSG_NOTICE([$fvmc_ac_config_publ_h  is unchanged])
  rm -f $fvmc_ac_config_publ_h-tmp
else
  mv $fvmc_ac_config_publ_h-tmp $fvmc_ac_config_publ_h
fi

unset fvmc_ac_config_publ_h
unset fvmc_ac_config_publ_upname

])dnl


# FVMC_AC_CONFIG_PUBL_VERBATIM([VERBATIM TEXT])
#---------------------------------------------
# Add text to config file

AC_DEFUN([FVMC_AC_CONFIG_PUBL_VERBATIM],[

AC_REQUIRE([FVMC_AC_CONFIG_PUBL_INIT])dnl

echo "$1" >> "$fvmc_ac_config_publ_h"-tmp
echo ""   >> "$fvmc_ac_config_publ_h"-tmp

])dnl


# FVMC_AC_CONFIG_PUBL_DEFINE(VARIABLE NAME, VALUE, [COMMENT])
#-----------------------------------------------------------
# Define variable

AC_DEFUN([FVMC_AC_CONFIG_PUBL_DEFINE],[

AC_REQUIRE([FVMC_AC_CONFIG_PUBL_INIT])dnl

unset fvmc_ac_lower
unset fvmc_ac_upper
if test "$3" != "" ; then
  echo "/* $3 */" >> "$fvmc_ac_config_publ_h"-tmp
fi
echo "#define $1 $2" >> "$fvmc_ac_config_publ_h"-tmp
echo >> "$fvmc_ac_config_publ_h"-tmp

])dnl


# FVMC_AC_CONFIG_PUBL_DEFINE_STRING(VARIABLE NAME, VALUE, [COMMENT])
#------------------------------------------------------------------
# Define string variable

AC_DEFUN([FVMC_AC_CONFIG_PUBL_DEFINE_STRING],[

AC_REQUIRE([FVMC_AC_CONFIG_PUBL_INIT])dnl

unset fvmc_ac_lower
unset fvmc_ac_upper
if test "$3" != "" ; then
  echo "/* $3 */" >> "$fvmc_ac_config_publ_h"-tmp
fi
echo '#define $1 "$2"' >> "$fvmc_ac_config_publ_h"-tmp
echo >> "$fvmc_ac_config_publ_h"-tmp

])dnl


# FVMC_AC_CONFIG_PUBL_SET(VARIABLE NAME, YES OR NO, [COMMENT])
#------------------------------------------------------------
# Set variable (define to 1 if second arg is yes, undefine if no )

AC_DEFUN([FVMC_AC_CONFIG_PUBL_SET],[

# First arg is variable name, second arg is value, optional third arg is comment

AC_REQUIRE([FVMC_AC_CONFIG_PUBL_INIT])dnl

unset fvmc_ac_lower
unset fvmc_ac_upper
if test "$3" != "" ; then
  echo "/* $3 */" >> "$fvmc_ac_config_publ_h"-tmp
fi
if test "$2" = "yes" ; then
  echo "#define $1 1" >> "$fvmc_ac_config_publ_h"-tmp
else
  echo "#undef $1" >> "$fvmc_ac_config_publ_h"-tmp
fi
echo >> "$fvmc_ac_config_publ_h"-tmp

])dnl

